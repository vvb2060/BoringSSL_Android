plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

def enableLTO = providers.gradleProperty("enableLTO")

android {
    compileSdk = 35
    buildToolsVersion = '35.0.0'
    ndkVersion = '27.0.12077973'
    namespace = 'io.github.vvb2060.ndk.boringssl'
    defaultConfig {
        minSdk = 21
        targetSdk = 35
        externalNativeBuild {
            ndkBuild {
                arguments += [ "-j${Runtime.runtime.availableProcessors()}" ]
                if (enableLTO.present) arguments += [ "enableLTO=1" ]
                abiFilters += [ "x86", "x86_64", "armeabi-v7a", "arm64-v8a", "riscv64" ]
            }
        }
    }

    externalNativeBuild {
        ndkBuild.path 'src/main/native/BoringSSL.mk'
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    buildFeatures {
        buildConfig = false
        prefabPublishing = true
    }

    prefab {
        ssl_static {
            headers "src/main/native/src/include"
        }
        crypto_static {
            headers "src/main/native/src/include"
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            group 'io.github.vvb2060.ndk'
            artifactId 'boringssl'
            if (enableLTO.present) {
                version '20241024-lto-ndk27'
            } else {
                version '20241024'
            }
            afterEvaluate {
                from components.release
            }
            pom {
                name = 'boringssl'
                description = 'boringssl static library prefab for android.'
                url = 'https://github.com/vvb2060/BoringSSL_Android'
                licenses {
                    license {
                        name = 'OpenSSL'
                        url = 'https://www.openssl.org/source/license.html'
                    }
                    license {
                        name = 'ISC'
                        url = 'https://github.com/google/boringssl/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        name = 'vvb2060'
                        url = 'https://github.com/vvb2060'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/vvb2060/BoringSSL_Android.git'
                    url = 'https://github.com/vvb2060/BoringSSL_Android'
                }
            }
        }
    }
    repositories {
        maven {
            name 'ossrh'
            url 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials(PasswordCredentials)
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    def secretKeyRingFile = findProperty("signing.secretKeyRingFile")

    if (secretKeyRingFile != null && file(secretKeyRingFile).exists()) {
        sign publishing.publications
    } else if (signingKey != null) {
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications
    }
}
